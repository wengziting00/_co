// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM8.hdl
/**
 * Memory of eight 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address, 
             a=w0, b=w1, c=w2, d=w3, 
             e=w4, f=w5, g=w6, h=w7);

    Register(in=in, load=w0, out=r0_out);
    Register(in=in, load=w1, out=r1_out);
    Register(in=in, load=w2, out=r2_out);
    Register(in=in, load=w3, out=r3_out);
    Register(in=in, load=w4, out=r4_out);
    Register(in=in, load=w5, out=r5_out);
    Register(in=in, load=w6, out=r6_out);
    Register(in=in, load=w7, out=r7_out);

    Mux8Way16(a=r0_out, b=r1_out, c=r2_out, d=r3_out, 
              e=r4_out, f=r5_out, g=r6_out, h=r7_out, 
              sel=address, out=out);
}
